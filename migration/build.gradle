buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath libs.flyway.database.postgresql
    }
}

plugins {
    id 'java'
    alias(libs.plugins.shadow)
    alias(libs.plugins.flyway)
}

version = '1.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(libs.flyway.commandline) {
        transitive = false
    }
    implementation libs.jansi
    implementation libs.jackson.dataformat.toml
    implementation libs.jackson.dataformat.xml

    implementation libs.flyway.core

    implementation libs.flyway.database.postgresql
    implementation libs.postgresql
}

shadowJar {
    mergeServiceFiles()
    zip64 true
    manifest {
        attributes 'Main-Class': 'org.flywaydb.commandline.Main',
                'Implementation-Title': 'Database Migration Runner', // 任意のわかりやすい名前に変更できます
                'Implementation-Version': project.version,           // 上部で定義している '1.0.1' が自動で入ります
                'Built-By': System.getProperty('user.name'),         // ビルドを実行したユーザー名
                'Build-Jdk': System.getProperty('java.version')      // ビルドに使用したJavaのバージョン
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:15432/template'
    user = 'myuser'
    password = 'secret'
    locations = [
            "classpath:db/migration/schema",
            "classpath:db/migration/data/${System.getenv('ENV') ?: 'dev'}"
    ]
}
